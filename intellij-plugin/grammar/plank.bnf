{
  parserClass="com.lorenzoog.jplank.intellijplugin.parser.PlankParser"
  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Plank"
  psiImplClassSuffix="Impl"
  psiPackage="com.lorenzoog.jplank.intellijplugin.psi"
  psiImplPackage="com.lorenzoog.jplank.intellijplugin.psi.impl"

  elementTypeHolderClass="com.lorenzoog.jplank.intellijplugin.psi.PlankTypes"
  elementTypeClass="com.lorenzoog.jplank.intellijplugin.psi.PlankElementType"
  tokenTypeClass="com.lorenzoog.jplank.intellijplugin.psi.PlankTokenType"

  psiImplUtilClass="com.lorenzoog.jplank.intellijplugin.psi.impl.PlankPsiImplUtil"
}

program ::= imports ? decl * EOF ;

imports ::= importDirective * ;
importDirective ::= IMPORT IDENTIFIER SEMICOLON ;

// typedef

parameter ::= IDENTIFIER COLON typeDef {
  implements="com.lorenzoog.jplank.intellijplugin.psi.PlankNamedElement"
  mixin="com.lorenzoog.jplank.intellijplugin.psi.impl.PlankNamedElementImpl"
  methods=[getName setName]
};

callableTypeDef ::= LPAREN ( typeDef ( COMMA typeDef ) * ) ? RPAREN ARROW_LEFT typeDef ;
nameTypeDef ::= IDENTIFIER {
  implements="com.lorenzoog.jplank.intellijplugin.psi.PlankNamedElement"
  mixin="com.lorenzoog.jplank.intellijplugin.psi.impl.PlankNamedElementImpl"
  methods=[getName setName]
};
pointerTypeDef ::= STAR typeDef ;
arrayTypeDef ::= LBRACKET typeDef RBRACKET ;
genericAccessTypeDef ::= APHOSTROPHE IDENTIFIER ;
genericTypeDef ::= IDENTIFIER LESS ( typeDef ( COMMA typeDef ) * ) ? GREATER {
  methods=[getReceiver]
};

typeDef ::= nameTypeDef
          | callableTypeDef
          | arrayTypeDef
          | genericAccessTypeDef
          | genericTypeDef
          | pointerTypeDef
          ;

// declarations
decl ::= letDecl WS *
       | funDecl WS *
       | nativeFunDecl WS *
       | classDecl WS *
       ;

// class
field ::= MUTABLE ? parameter {
  methods=[getMutable]
};

classDecl ::= TYPE IDENTIFIER EQUAL LBRACE ( field ( COMMA field ) * ) ? RBRACE SEMICOLON {
  implements="com.lorenzoog.jplank.intellijplugin.psi.PlankNamedElement"
  methods=[getName setName getFields]
};

// let decl
letDecl ::= LET MUTABLE? IDENTIFIER EQUAL expr SEMICOLON
          | LET MUTABLE? IDENTIFIER COLON typeDef EQUAL expr SEMICOLON {
            methods=[getName getMutable]
          }
          ;

funHeader ::= FUN IDENTIFIER LPAREN ( parameter ( COMMA parameter ) * ) ? RPAREN COLON typeDef {
  implements="com.lorenzoog.jplank.intellijplugin.psi.PlankNamedElement"
  methods=[getName setName]
};

// functions
nativeFunDecl ::= NATIVE funHeader SEMICOLON ;
funDecl ::= funHeader LBRACE stmt * RBRACE  {
  implements="com.lorenzoog.jplank.intellijplugin.psi.PsiScope"
  mixin="com.lorenzoog.jplank.intellijplugin.psi.impl.PlankDelegateScopedImpl"
  methods=[
    declare
    define
    expand
    findFunction
    findStruct
    findType
    findVariable
    lookup
  ]
} ;

// statements
exprStmt ::= expr SEMICOLON ;
returnStmt ::= RETURN expr ? SEMICOLON ;

stmt ::= decl
       | ifExpr WS *
       | exprStmt WS *
       | returnStmt WS *
       ;

// if expression
thenBranch ::= LBRACE ( stmt * ) ? RBRACE {
  implements="com.lorenzoog.jplank.intellijplugin.psi.PsiScope"
  mixin="com.lorenzoog.jplank.intellijplugin.psi.impl.PlankDelegateScopedImpl"
  methods=[
    declare
    define
    expand
    findFunction
    findStruct
    findType
    findVariable
    lookup
  ]
} ;

elseBranch ::= ELSE expr
             | ELSE LBRACE ( stmt * ) ? RBRACE {
  implements="com.lorenzoog.jplank.intellijplugin.psi.PsiScope"
  mixin="com.lorenzoog.jplank.intellijplugin.psi.impl.PlankDelegateScopedImpl"
  methods=[
    declare
    define
    expand
    findFunction
    findStruct
    findType
    findVariable
    lookup
  ]
} ;

ifExpr ::= IF LPAREN expr RPAREN thenBranch elseBranch? ;

// assign expression
assign ::= IDENTIFIER EQUAL assignExpr {
  methods=[getReceiver getValue]
}

set ::= ( callExpr DOT ) ? IDENTIFIER EQUAL assignExpr {
  methods=[getReceiver getValue]
}

assignExpr ::= assign | set | equality {
  methods=[getValue]
};

equality ::= comparison ( ( EQUAL_EQUAL | BANG_EQUAL ) comparison ) * {
  methods=[getLhs getRightmostOperands]
};

comparison ::= term ( ( GREATER | GREATER_EQUAL | LESS | LESS_EQUAL ) term ) * {
  methods=[getLhs getRightmostOperands]
};

term ::= factor ( ( MINUS | PLUS | CONCAT ) factor ) * {
  methods=[getLhs getRightmostOperands]
};

factor ::= unary ( ( STAR | SLASH ) unary ) * {
  methods=[getLhs getRightmostOperands]
};

unary ::= ( BANG | MINUS ) unary | callExpr {
  methods=[getOperator]
};

// call expression
argument ::= IDENTIFIER COLON expr ;
instanceArguments ::= LBRACE ( argument ( COMMA argument ) * ) ? RBRACE {
  methods=[getArguments]
};

arguments ::= LPAREN ( expr ( COMMA expr ) * ) ? RPAREN ;
get ::= DOT IDENTIFIER {
  implements="com.lorenzoog.jplank.intellijplugin.psi.PlankNamedElement"
  mixin="com.lorenzoog.jplank.intellijplugin.psi.impl.PlankNamedElementImpl"
  methods=[getName setName]
};

callExpr ::= pointer ( instanceArguments | ( arguments | get ) *  ) ;

// primary expression
pointer ::= STAR expr
          | AMPERSAND expr
          | primary
          ;

identifierExpr ::= IDENTIFIER {
  implements="com.lorenzoog.jplank.intellijplugin.psi.PlankNamedElement"
  mixin="com.lorenzoog.jplank.intellijplugin.psi.impl.PlankNamedElementImpl"
  methods=[getName setName]
};

primary ::= STRING
          | INT
          | DECIMAL
          | TRUE
          | FALSE
          | LPAREN expr RPAREN
          | identifierExpr
          ;

// expressions
sizeofExpr ::= SIZEOF typeDef ;

expr ::= assignExpr
       | ifExpr
       | sizeofExpr
       ;

